import com.github.jengelman.gradle.plugins.shadow.transformers.IncludeResourceTransformer

// apply the script plugins
plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '1.2.1'
}
// project information
group = 'org.naaccr'
version = file('VERSION').text
description = 'Attempt from the NAACCR XML Task Force to map the NAACCR flat-file format to an XML one.'

// repository for the dependencies
repositories {
    mavenCentral()
}

// project dependencies
dependencies {
    compile 'commons-io:commons-io:2.4'
    compile 'org.apache.commons:commons-lang3:3.3.2'
    compile 'com.thoughtworks.xstream:xstream:1.4.7'
    compile 'org.tukaani:xz:1.5'
    testCompile 'junit:junit:4.11'
    testCompile 'au.com.bytecode:opencsv:2.4'
}

// customize the manifest
jar {
    from 'LICENSE'
    from 'VERSION'
    from 'changelog.txt'
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': version,
                'Main-Class': 'org.naaccr.xml.gui.Standalone',
                'Class-Path': configurations.compile.collect { it.getName() }.join(' '))
    }
}

// I really shouldn't have to do this, but for some reason, shadowJar doesn't copy the few extra files I add to the single JAR...
shadowJar {
    transform(IncludeResourceTransformer) {
        file = project.file('changelog.txt')
        resource = 'changelog.txt'
    }
    transform(IncludeResourceTransformer) {
        file = project.file('LICENSE')
        resource = 'LICENSE'
    }
    transform(IncludeResourceTransformer) {
        file = project.file('VERSION')
        resource = 'VERSION'
    }
    exclude('XMLPULL_1_1_3_1_VERSION')
    exclude('XPP3_1.1.4c_MIN_VERSION')
}

// Gradle wrapper, this allows to build the project without having to install Gradle...
task wrapper(type: Wrapper) {
    gradleVersion = '2.2'
}
